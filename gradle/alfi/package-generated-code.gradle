ext {
    codeGenTargetDir = "$buildDir/code-gen"
}

task packageGeneratedCode {
    group 'alfi-samples'


    doFirst {
        file(codeGenTargetDir).deleteDir()
        file(codeGenTargetDir).mkdir()
        file("$codeGenTargetDir/hamster").mkdir()
        file("$codeGenTargetDir/swc").mkdir()
        file("$codeGenTargetDir/standardModelLibrary").mkdir()
    }

    doLast {
        def samplesArtifacts = "$buildDir/artifacts/mps-alfi-samples/mps-alfi-samples/languages"
        def alfiArtifacts = "$buildDir/mps-bundle/dependencies/alfi/languages/alfi-modules"

        copyGeneratedCode("$samplesArtifacts/hamster-simulator-language/HamsterSimulatorLanguage.sandbox-src.jar", "hamster")
        copyGeneratedCode("$samplesArtifacts/software-component-language/SoftwareComponentLanguage.sandbox-src.jar", "swc")

        copyGeneratedCode("$alfiArtifacts/alfi.StandardModelLibrary-src.jar", "standardModelLibrary")
        ["hamster", "swc"].each { dirName ->
            copy {
                from "$codeGenTargetDir/standardModelLibrary/alf/testing"
                into "$codeGenTargetDir/$dirName/alf"
                // postprocess the files
                eachFile { details ->
                    def content = details.file.text.replaceAll('@primitive', '')
                    details.file.text = content
                }
            }
        }
        file("$codeGenTargetDir/standardModelLibrary/alf").deleteDir()
    }
}

private def copyGeneratedCode(String sourcesArtifact, String targetDirName) {
    def targetDir = "$ext.codeGenTargetDir/$targetDirName"

    copy {
        from zipTree(sourcesArtifact)
        into "$targetDir/cpp"
        include('**/*.cpp', '**/*.h', '**/*.hpp', '**/*.cmake')
    }
    copy {
        from "$projectDir/scripts/$targetDirName/cpp"
        into "$targetDir/cpp"
    }

    copy {
        from zipTree(sourcesArtifact)
        into "$targetDir/java/src/main/java"
        include('**/*.java')
    }
    copy {
        from "$projectDir/scripts/$targetDirName/java"
        into "$targetDir/java"
    }
    moveJavaSourcesToTestDir(targetDir)

    copy {
        from zipTree(sourcesArtifact)
        into "$targetDir/alf"
        include('**/*.alf')
    }
    copy {
        from "$projectDir/scripts/$targetDirName/alf"
        into "$targetDir/alf"
    }
    squashIntermediateDirs(new File("$targetDir/alf"))

    copy {
        from zipTree(sourcesArtifact)
        into "$targetDir/cs"
        include('**/*.cs', '**/*.props')
    }
    copy {
        from "$projectDir/scripts/$targetDirName/cs"
        into "$targetDir/cs"
    }

    def dirsToDelete = []
    file(targetDir).eachFileRecurse { if (onlyContainsEmptyDirs(it) && !onlyContainsEmptyDirs(it.parentFile)) { dirsToDelete.add(it) } }
    dirsToDelete.each { it.deleteDir() }
}

private static boolean onlyContainsEmptyDirs(File dir) {
    if (!dir.isDirectory()) {
        return false
    }

    def subFiles = dir.listFiles()
    if (subFiles.length == 0) {
        return true
    }

    return subFiles.every {
        it.isDirectory() && onlyContainsEmptyDirs(it)
    }
}

private def squashIntermediateDirs(File baseDir, boolean root = true) {
    if (!baseDir.isDirectory()) {
        return
    }

    baseDir.eachFile { file ->
        if (file.isDirectory()) {
            squashIntermediateDirs(file, false)
        }
    }

    if (root) {
        return
    }

    def subFiles = baseDir.listFiles()

    if (subFiles.every { it.isDirectory() }) {
        subFiles.each { subDir ->
            subDir.eachFile { subFile ->
                subFile.renameTo(new File(baseDir.parentFile, subFile.name))
            }
            subDir.deleteDir()
        }
        baseDir.deleteDir()
    }
}

private void moveJavaSourcesToTestDir(String targetDir) {
    def javaDir = "$targetDir/java/src/main/java"
    def testDir = "$targetDir/java/src/test/java"

    file(javaDir).eachFileRecurse { file ->
        if (file.isFile() && file.text.contains("org.junit.jupiter")) {
            def relativePath = file.path.substring(javaDir.length() + 1)
            def testFile = new File(testDir, relativePath)
            testFile.parentFile.mkdirs() // Ensure the target directory exists
            file.renameTo(testFile)
            println "Moved $file to $testFile"
        }

    }
}
